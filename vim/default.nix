# Vim, with a set of extra packages (extraPackages) and a custom vimrc
# (./vimrc). The final vimrc file is generated by vimUtils.vimrcFile and
# bundles all the packages with the custom vimrc.
{ git
, coreutils
, tmux
, symlinkJoin
, makeWrapper
, vim_configurable
, vimUtils
, vimPlugins
, lib
, ctags
, python37
, stdenv
}:
let

  extraPackages = with vimPlugins;
    [
      ctrlp
      elm-vim
      fugitive
      nerdcommenter
      nerdtree
      purescript-vim
      surround
      syntastic
      vim-airline
      vim-colorschemes
      vim-easymotion
      vim-indent-guides
      vim-markdown
      vim-multiple-cursors
      vim-nix
      vim-toml
      vim-tmux-navigator
      vim-trailing-whitespace
      vimproc
      YouCompleteMe
    ];
  customRC = vimUtils.vimrcFile
    {
      customRC = builtins.readFile ./vimrc;
      packages.mvc.start = extraPackages;
    };
in
symlinkJoin {
  name = "vim";
  buildInputs = [ makeWrapper ];
  postBuild = ''
    wrapProgram "$out/bin/vim" \
    --add-flags "-u ${customRC}" \
      --prefix PATH ':' '${coreutils}/bin:${ctags}/bin:${git}/bin:${tmux}/bin'
  '';
  paths = [ vim_configurable ];
}
